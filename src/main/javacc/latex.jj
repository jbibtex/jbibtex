/*
 * Copyright (c) 2012 University of Tartu
 */
options {
	GENERATE_BOILERPLATE = false;
	JAVA_TEMPLATE_TYPE = "modern";
	JDK_VERSION = "1.5";
	KEEP_LINE_COLUMN = true;
	STATIC = false;
}

PARSER_BEGIN(LaTeXParser)
package org.jbibtex;

import java.io.Reader;
import java.io.StringReader;
import java.util.ArrayList;
import java.util.List;

public class LaTeXParser {

	public LaTeXParser() throws ParseException {
		this("");
	}

	/**
	 * @see LaTeXParser#parse(Reader)
	 */
	public List<LaTeXObject> parse(String string) throws ParseException, TokenMgrException {
		return parse(new StringReader(string));
	}

	/**
	 * @throws TokenMgrException If the input contains invalid characters
	 *
	 * @see CharacterFilterReader
	 */
	public List<LaTeXObject> parse(Reader reader) throws ParseException, TokenMgrException {
		ReInit(new StreamProvider(reader));

		return LaTeX();
	}
}
PARSER_END(LaTeXParser)

<DEFAULT>
TOKEN : 
{
	< BACKSLASH : "\\" >
	|
	< LBRACE : "{" >
	|
	< RBRACE : "}" >
}

<DEFAULT>
TOKEN :
{
	< WHITESPACE : ["\t", "\n", " "] >
	|
	< SPECIAL : ["#", "$", "%", "&", "^", "_", "~"] >
	|
	< NON_SPECIAL : ["!"-"\"", "\'"-"/", ":"-"@", "[", "]", "`", "|", "\u007f"] >
}

<DEFAULT>
TOKEN :
{
	< ASCII_LETTER : ["a"-"z", "A"-"Z"] >
	|
	< ASCII_DIGIT : ["0"-"9"] >
	|
	< LATIN_SUPPLEMENT : ["\u00a0"-"\u00ff"] >
	|
	< UNICODE_LETTER : ["\u0100"-"\u1fff", "\u20ac", "\u2122"] >
}

private List<LaTeXObject> LaTeX() :
{
	List<LaTeXObject> objects;
}
{
	objects = ObjectList() <EOF>

	{
		return objects;
	}
}

private List<LaTeXObject> ObjectList() :
{
	List<LaTeXObject> objects = new ArrayList<LaTeXObject>();

	LaTeXObject object;
}
{
	( object = Object() { objects.add(object); } )*

	{
		return objects;
	}
}

private LaTeXObject Object() :
{
	LaTeXObject object;
}
{
	(
		object = Command()
		|
		object = Group()
		|
		object = String()
	)

	{
		return object;
	}
}

private LaTeXCommand Command() :
{
	String name;
}
{
	<BACKSLASH>
	(
		name = Name() Whitespace()
		|
		name = SpecialCharacter()
		|
		name = NonSpecialCharacter()
	)

	{
		return new LaTeXCommand(name);
	}
}

private String Name() :
{
	StringBuilder sb = new StringBuilder(16);

	Token token;
}
{
	token = <ASCII_LETTER> { sb.append(token.image); } ( LOOKAHEAD( <ASCII_LETTER> ) token = <ASCII_LETTER> { sb.append(token.image); } )*

	{
		return sb.toString();
	}
}

private void Whitespace() :
{}
{
	( LOOKAHEAD( WhitespaceCharacter() ) WhitespaceCharacter() )*
}

private String WhitespaceCharacter() :
{
	Token token;
}
{
	token = <WHITESPACE>

	{
		return token.image;
	}
}

private String SpecialCharacter() :
{
	Token token;
}
{
	(
		token = <BACKSLASH>
		|
		token = <LBRACE>
		|
		token = <RBRACE>
		|
		token = <SPECIAL>
	)

	{
		return token.image;
	}
}

private String NonSpecialCharacter() :
{
	Token token;
}
{
	token = <NON_SPECIAL>

	{
		return token.image;
	}
}

private LaTeXGroup Group() :
{
	List<LaTeXObject> objects;
}
{
	<LBRACE> objects = ObjectList() <RBRACE>

	{
		return new LaTeXGroup(objects);
	}
}

private LaTeXString String() :
{
	StringBuilder sb = new StringBuilder(64);

	String string;
}
{
	string = TextCharacter() { sb.append(string); } ( LOOKAHEAD( TextCharacter() ) string = TextCharacter() { sb.append(string); } )*

	{
		return new LaTeXString(sb.toString());
	}
}

private String TextCharacter() :
{
	Token token;
}
{
	(
		token = <WHITESPACE>
		|
		token = <NON_SPECIAL>
		|
		token = <ASCII_LETTER>
		|
		token = <ASCII_DIGIT>
		|
		token = <LATIN_SUPPLEMENT>
		|
		token = <UNICODE_LETTER>
	)

	{
		return token.image;
	}
}