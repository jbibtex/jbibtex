/*
 * Copyright (c) 2012 University of Tartu
 */
options {
	JAVA_TEMPLATE_TYPE = "modern";
	JDK_VERSION = "1.5";
	KEEP_LINE_COLUMN = true;
	STATIC = false;
}

PARSER_BEGIN(BibTeXParser)
package org.jbibtex;

import java.io.FileReader;
import java.io.Reader;
import java.util.Collections;
import java.util.ArrayList;
import java.util.List;
import java.util.Map;

public class BibTeXParser {

	private BibTeXDatabase database = null;

	private List<Exception> exceptions = new ArrayList<Exception>();

	private long id = 1L;


	public BibTeXParser() throws ParseException, TokenMgrException {
		this("");
	}

	/**
	 * Parses the BibTeX document in "normal" mode.
	 * The parser throws an exception is an error condition is detected.
	 *
	 * @throws ObjectResolutionException If the database is inconsistent.
	 *
	 * @throws TokenMgrException If a token cannot be produced.
	 * @throws ParseException If the produced sequence of tokens cannot be consumed.
	 *
	 * @see CharacterFilterReader
	 */
	public BibTeXDatabase parse(Reader reader) throws ObjectResolutionException, ParseException, TokenMgrException {
		ReInit(new StreamProvider(reader));

		token_source.SwitchTo(IN_HEADER);

		return Database(false);
	}

	/**
	 * Parses the BibTeX document in "error recovery" mode.
	 * The parser does its best to recover from typical error conditions by skipping the problematic object definition.
	 * The parser maintains a {@link #getExceptions() list of exceptions}. This list is cleared when a parse method is invoked again.
	 *
	 * @throws ObjectResolutionException If the database is inconsistent.
	 *
	 * @see CharacterFilterReader
	 */
	public BibTeXDatabase parseFully(Reader reader) throws ObjectResolutionException {
		ReInit(new StreamProvider(reader));

		token_source.SwitchTo(IN_HEADER);

		try {
			return Database(true);
		} catch(TokenMgrException tme){
			throw new RuntimeException(tme);
		} catch(ParseException pe){
			throw new RuntimeException(pe);
		}
	}

	/**
	 * Checks the <code>string</code> argument before constructing a new {@link ReferenceValue} instance.
	 * The default behaviour is to prohibit unresolved references.
	 *
	 * @throws ObjectResolutionException If the <code>string</code> is <code>null</code>.
	 *
	 * @see BibTeXDatabase#resolveString(Key)
	 * @see BibTeXParser#getMacros()
	 */
	public void checkStringResolution(Key key, BibTeXString string){

		if(string == null){
			throw new ObjectResolutionException(key);
		}
	}

	private void resolveCrossReferences(){
		Map<Key, BibTeXEntry> entries = getDatabase().getEntries();

		for(BibTeXEntry entry : entries.values()){
			Map<Key, Value> fields = entry.getFields();

			Value value = fields.get(BibTeXEntry.KEY_CROSSREF);
			if((value == null) || (value instanceof CrossReferenceValue)){
				continue;
			}

			Key key = new Key(value.toUserString());

			BibTeXEntry object = getDatabase().resolveEntry(key);
			checkCrossReferenceResolution(key, object);

			entry.addField(BibTeXEntry.KEY_CROSSREF, new CrossReferenceValue(value, object));
		}
	}

	/**
	 * Checks the <code>entry</code> argument before constructing a new {@link CrossReferenceValue} instance.
	 * The default behaviour is to prohibit unresolved references.
	 *
	 * @throws ObjectResolutionException If the <code>entry</code> is <code>null</code>.
	 *
	 * @see BibTeXDatabase#resolveEntry(Key)
	 */
	public void checkCrossReferenceResolution(Key key, BibTeXEntry entry){

		if(entry == null){
			throw new ObjectResolutionException(key);
		}
	}

	/**
	 * Generates a key value for an anonymous BibTeX bibliography database entry.
	 *
	 * @see BibTeXEntry#getKey
	 * @see BibTeXDatabase#resolveEntry(Key)
	 */
	public String nextKey(){
		return String.valueOf("jbibtex-" + this.id++);
	}

	public BibTeXDatabase getDatabase(){
		return this.database;
	}

	private void setDatabase(BibTeXDatabase database){
		this.database = database;
	}

	public List<Exception> getExceptions(){
		return Collections.unmodifiableList(this.exceptions);
	}

	static
	public KeyMap<BibTeXString> getMacros(){
		return BibTeXParser.macros;
	}

	static
	public void addMacro(String key, String value){
		addMacro(new BibTeXString(new Key(key), new StringValue(value, StringValue.Style.BRACED)));
	}

	static
	public void addMacro(BibTeXString macro){
		BibTeXParser.macros.put(macro.getKey(), macro);
	}

	static
	public void removeMacro(BibTeXString macro){
		BibTeXParser.macros.remove(macro.getKey());
	}

	static
	private boolean isDigitString(String string){

		for(int i = 0; i < string.length(); i++){
			char c = string.charAt(i);

			// Only ASCII digits from '0' to '9'
			if(c < '0' || c > '9'){
				return false;
			}
		}
		
		return (string.length() > 0);
	}

	private static final KeyMap<BibTeXString> macros = new KeyMap<BibTeXString>();

	static {
		addMacro("jan", "January");
		addMacro("feb", "February");
		addMacro("mar", "March");
		addMacro("apr", "April");
		addMacro("may", "May");
		addMacro("jun", "June");
		addMacro("jul", "July");
		addMacro("aug", "August");
		addMacro("sep", "September");
		addMacro("oct", "October");
		addMacro("nov", "November");
		addMacro("dec", "December");
	}


	static
	private class Field {

		private Key key = null;

		private Value value = null;


		private Field(Key key, Value value){
			setKey(key);
			setValue(value);
		}

		public Key getKey(){
			return this.key;
		}

		private void setKey(Key key){
			this.key = key;
		}

		public Value getValue(){
			return this.value;
		}

		private void setValue(Value value){
			this.value = value;
		}
	}
}
PARSER_END(BibTeXParser)

TOKEN_MGR_DECLS : {

	void backup(int n){
		input_stream.backup(n);
	}
}

<IN_HEADER>
SKIP :
{
	< ( ~["%", "@"] )* > : DEFAULT
}

<DEFAULT>
MORE :
{
	"%" : IN_INLINE_COMMENT
}

<IN_INLINE_COMMENT>
SPECIAL_TOKEN :
{
	< INLINE_COMMENT : ( ~["\r", "\n"] )* ("\r" | "\n" | "\r\n")? > : DEFAULT
}

<IN_INLINE_COMMENT>
MORE :
{
	< ~[] >
}

<IN_LITERAL>
TOKEN :
{
	< TEXT : ~[] >
}

<DEFAULT>
SKIP :
{
	"\t"
	|
	"\n"
	|
	"\f"
	|
	"\r"
	|
	" "
}

<DEFAULT>
TOKEN :
{
	< AT : "@" >
	|
	< COMMA : "," >
	|
	< EQUALS : "=" >
	|
	< HASH : "#" >
	|
	< LBRACE : "{">
	|
	< LPAREN : "(" >
	|
	< QUOTE : "\"" >
	|
	< RBRACE : "}" >
	|
	< RPAREN : ")" > 
}

<DEFAULT>
TOKEN :
{
	< COMMENT : ( ["c", "C"] ["o", "O"] ["m", "M"] ["m", "M"] ["e", "E"] ["n", "N"] ["t", "T"] ) >
	|
	< INCLUDE : ( ["i", "I"] ["n", "N"] ["c", "C"] ["l", "L"] ["u", "U"] ["d", "D"] ["e", "E"] ) >
	|
	< PREAMBLE : ( ["p", "P"] ["r", "R"] ["e", "E"] ["a", "A"] ["m", "M"] ["b", "B"] ["l", "L"] ["e", "E"] ) >
	|
	< STRING : ( ["s", "S"] ["t", "T"] ["r", "R"] ["i", "I"] ["n", "N"] ["g", "G"] ) >
}

<DEFAULT>
TOKEN :
{
	< NAME : ( <ASCII_LETTER> | <ASCII_DIGIT> | <LATIN_LETTER> | <UNICODE_LETTER> | <NAME_START_SYMBOL> ) ( <ASCII_LETTER> | <ASCII_DIGIT> | <LATIN_LETTER> | <UNICODE_LETTER> | <NAME_CONTINUATION_SYMBOL> )* >
	|
	< #ASCII_LETTER : ["a"-"z", "A"-"Z"] >
	|
	< #ASCII_DIGIT : ["0"-"9"] >
	|
	< #LATIN_LETTER : ["\u00c0"-"\u00d6", "\u00d8"-"\u00f6", "\u00f8"-"\u00ff"] >
	|
	< #UNICODE_LETTER : ["\u0100"-"\u1fff", "\u20ac", "\u2122"] >
	|
	< #NAME_START_SYMBOL : ["(", "-", "_"] >
	|
	< #NAME_CONTINUATION_SYMBOL : ["!", "'", "(", ")", "+", "-", ".", "/", ":", "?", "_"] >
}

private BibTeXDatabase Database(boolean performErrorRecovery) :
{
	BibTeXDatabase database = new BibTeXDatabase();

	BibTeXObject object = null;
}
{
	{
		setDatabase(database);

		this.exceptions.clear();
	}

	(
		try {
			<COMMA>
			|
			object = Object() { database.addObject(object); }
		} catch(TokenMgrException tme){

			if(!performErrorRecovery){
				throw tme;
			}

			this.exceptions.add(tme);

			SkipTo("@");
		} catch(ParseException pe){

			if(!performErrorRecovery){
				throw pe;
			}

			this.exceptions.add(pe);

			SkipTo("@");
		}
	)* <EOF>

	{
		resolveCrossReferences();

		setDatabase(null);

		return database;
	}
}

private BibTeXObject Object() :
{
	BibTeXObject object;
}
{
	(
		LOOKAHEAD( <AT> <COMMENT> )
		object = Comment()
		|
		LOOKAHEAD( <AT> <INCLUDE> )
		object = Include()
		|
		LOOKAHEAD( <AT> <PREAMBLE> )
		object = Preamble()
		|
		LOOKAHEAD( <AT> <STRING> )
		object = String()
		|
		object = Entry()
	)

	{
		return object;
	}
}

private BibTeXComment Comment() :
{
	String string;
}
{
	<AT> <COMMENT>
	(
		<LBRACE> string = Literal(1, "}")
	)

	{
		return new BibTeXComment(new StringValue(string, StringValue.Style.BRACED));
	}
}

private BibTeXInclude Include():
{
	String string;
}
{
	<AT> <INCLUDE>
	(
		<LBRACE> string = Literal(1, "}")
	)

	{
		BibTeXDatabase database;

		try {
			Reader reader = new FileReader(string);

			try {
				BibTeXParser parser = new BibTeXParser();

				database = parser.parse(reader);
			} finally {
				reader.close();
			}
		} catch(Exception e){
			throw new ParseException(e.getMessage());
		}

		return new BibTeXInclude(new StringValue(string, StringValue.Style.BRACED), database);
	}
}

private BibTeXPreamble Preamble() :
{
	Value value;
}
{
	<AT> <PREAMBLE>
	(
		<LPAREN> value = Value() <RPAREN>
		|
		<LBRACE> value = Value() <RBRACE>
	)

	{
		return new BibTeXPreamble(value);
	}
}

private BibTeXString String() :
{
	Field field;
}
{
	<AT> <STRING>
	(
		<LPAREN> field = Assignment() <RPAREN>
		|
		<LBRACE> field = Assignment() <RBRACE>
	)

	{
		return new BibTeXString(field.getKey(), field.getValue());
	}
}

private BibTeXEntry Entry() :
{
	Token type;

	Token key = null;

	List<Field> fields;
}
{
	<AT> type = <NAME>
	(
		<LPAREN> 
		(
			LOOKAHEAD( Identifier() <COMMA> )
			key = Identifier() <COMMA> fields = AssignmentList()
			|
			( <COMMA> )? fields = AssignmentList()
		)
		( <COMMA> )? <RPAREN>
		|
		<LBRACE> 
		(
			LOOKAHEAD( Identifier() <COMMA> )
			key = Identifier() <COMMA> fields = AssignmentList()
			|
			( <COMMA> )? fields = AssignmentList()
		)
		( <COMMA> )? <RBRACE>
	)

	{
		BibTeXEntry entry = new BibTeXEntry(new Key(type.image), key != null ? new Key(key.image) : new Key(nextKey()));

		for(Field field : fields){
			entry.addField(field.getKey(), field.getValue());
		}

		return entry;
	}
}

private Field Assignment() :
{
	Key key;

	Value value;
}
{
	key = Key() <EQUALS> value = Value()

	{
		return new Field(key, value);
	}
}

private List<Field> AssignmentList() :
{
	List<Field> fields = new ArrayList<Field>();

	Field field;
}
{
	field = Assignment() { fields.add(field); } ( LOOKAHEAD( <COMMA> Name() ) <COMMA> field = Assignment() { fields.add(field); } )*

	{
		return fields;
	}
}

private Token Identifier() :
{
	Token token;
}
{
	(
		token = Name()
	)

	{
		return token;
	}
}

private Token Name() :
{
	Token token;
}
{
	(
		token = <COMMENT>
		|
		token = <INCLUDE>
		|
		token = <PREAMBLE>
		|
		token = <STRING>
		|
		token = <NAME>
	)

	{
		return token;
	}
}

private Key Key() :
{
	Token token;
}
{
	token = Name()

	{
		return new Key(token.image);
	}
}

private Value Value() :
{
	Value left;

	Value right = null;
}
{
	left = SimpleValue() ( <HASH> right = Value() )?

	{
		if(right != null){
			return new ConcateValue(left, right);
		}

		return left;
	}
}

private Value SimpleValue() :
{
	String string;

	Token token;

	Value result = null;
}
{
	(
		<LBRACE> string = Literal(1, "}")
		{
			result = new StringValue(string, StringValue.Style.BRACED);
		}
		|
		<QUOTE> string = Literal(0, "\"")
		{
			result = new StringValue(string, StringValue.Style.QUOTED);
		}
		|
		token = Name()
		{
			boolean digits = isDigitString(token.image);

			if(!digits){
				KeyValue value = new KeyValue(token.image);

				Key key = value.toKey();

				BibTeXString object = getDatabase().resolveString(key);
				if(object == null){
					object = (BibTeXParser.getMacros()).get(key);
				}

				checkStringResolution(key, object);

				result = new ReferenceValue(value, object);
			} else

			{
				result = new DigitStringValue(token.image);
			}
		}
	)
	{
		if(result == null){
			throw new ParseException();
		}

		return result;
	}
}

JAVACODE
private void SkipTo(String symbol) throws ParseException {

	if(symbol == null){
		throw new IllegalArgumentException();
	}

	SetState(IN_LITERAL);

	for(Token prevToken = null, token = getNextToken(); true; prevToken = token, token = getNextToken()){

		if(token.kind == BibTeXParserConstants.EOF){
			throw new ParseException();
		} // End if

		if(symbol.equals(token.image)){
			break;
		}
	}

	token_source.backup(symbol.length());

	SetState(DEFAULT);
}

JAVACODE
private String Literal(int braceLevel, String delimiter) throws ParseException {
	StringBuilder sb = new StringBuilder(64);

	if(braceLevel < 0){
		throw new IllegalArgumentException();
	}

	SetState(IN_LITERAL);

	for(Token prevToken = null, token = getNextToken(); true; prevToken = token, token = getNextToken()){

		if(token.kind == BibTeXParserConstants.EOF){
			throw new ParseException();
		}

		boolean escaped = (prevToken != null && "\\".equals(prevToken.image));

		if(!escaped){

			if("{".equals(token.image)){
				braceLevel++;
			} else

			if("}".equals(token.image)){
				braceLevel--;
			}
		} // End if

		if(delimiter.equals(token.image) && braceLevel == 0){
			break;
		}

		sb.append(token.image);
	}

	SetState(DEFAULT);

	return StringUtil.removeIndent(sb.toString());
}

/**
 * JavaCC hack for doing lexical state transitions syntactically
 *
 * http://www.engr.mun.ca/~theo/JavaCC-FAQ/SetState.txt
 */
JAVACODE
private void SetState(int state) {
	if (state != token_source.curLexState) {
		Token root = new Token(), last=root;
		root.next = null;

		// First, we build a list of tokens to push back, in backwards order
		while (token.next != null) {
			Token t = token;
			// Find the token whose token.next is the last in the chain
			while (t.next != null && t.next.next != null)
				t = t.next;

			// put it at the end of the new chain
			last.next = t.next;
			last = t.next;

			// If there are special tokens, these go before the regular tokens,
			// so we want to push them back onto the input stream in the order
			// we find them along the specialToken chain.
			if (t.next.specialToken != null) {
				Token tt=t.next.specialToken;
				while (tt != null) {
					last.next = tt;
					last = tt;
					tt.next = null;
					tt = tt.specialToken;
				}
			}
			t.next = null;
		};

		while (root.next != null) {
			token_source.backup(root.next.image.length());
			root.next = root.next.next;
		}
		jj_ntk = -1;
		token_source.SwitchTo(state);
	}
}